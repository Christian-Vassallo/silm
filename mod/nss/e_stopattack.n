extern("inc_events")
extern("inc_cdb")
extern("inc_scorco")
extern("inc_nwnx_func")
#include "factions.nh"

bool is_faction_commoner(object creature) { return GetFactionID(creature) == FACTION_COMMONER; }
bool is_faction_defender(object creature) { return GetFactionID(creature) == FACTION_DEFENDER; }
bool is_faction_merchant(object creature) { return GetFactionID(creature) == FACTION_MERCHANT; }
bool is_faction_hostile(object creature) { return GetFactionID(creature) == FACTION_HOSTILE; }

void main() {
	struct EventInfo e = GetEventInfo();
	struct EventArguments a = GetEventArguments(e.serial);

	object attacker, attackee;

	if (e.ev == "pc_attack") {
		attacker = e.actor;
		attackee = e.actedon;
	} else if (e.ev == "creature_attacked") {
		attacker = e.actor;
		attackee = e.runnable;
	} else if (e.ev == "creature_damaged") {
		attacker = e.actor;
		attackee = e.runnable;
	} else
		return;

	if (is_dm(attacker))
		return;

	if (is_pc(attacker) && is_pc(attackee))
		return;

	if (
		(is_faction_commoner(attacker) && is_faction_defender(attackee)) ||
		(is_faction_commoner(attacker) && is_faction_merchant(attackee)) ||

		(is_faction_defender(attacker) && is_faction_commoner(attackee)) ||
		(is_faction_defender(attacker) && is_faction_merchant(attackee)) ||

		(is_faction_merchant(attacker) && is_faction_commoner(attackee)) ||
		(is_faction_merchant(attacker) && is_faction_defender(attackee)) ||

		(is_pc(attacker) && is_faction_commoner(attackee)) ||
		(is_pc(attacker) && is_faction_defender(attackee)) ||
		(is_pc(attacker) && is_faction_merchant(attackee))
	) {
		SetEventResult(EVENT_RESULT_SUPPRESS | EVENT_RESULT_STOP);

		AssignCommand(attacker, ClearAllActions(1));
		AssignCommand(attackee, ClearAllActions(1));

		ClearPersonalReputation(attacker, attackee);
		ClearPersonalReputation(attackee, attacker);

		SendMessageToAllDMs("[stopattack] Stopped " + otoa(attacker) + " from attacking " +
			otoa(attackee) + " because that would mess up the standard factions.");
		if (GetIsPC(attacker))
			SendMessageToPC(attacker, "Du kannst " + GetName(attackee) +
				" leider nicht angreifen; dies wuerde die Fraktionen zermuerben." +
				" Sprich mit einem SL fuer Rollenspiel in diese Richtung."
			);
	}
}
