extern("_gen")
extern("inc_party")

#define PARTY_REJOIN_DIFF 600

void main()
{
	struct EventInfo e = GetEventInfo();

	if (e.ev == "player_login") {
		if (is_dm(e.runnable))
			return;

		int p = GetLocalInt(e.runnable, "faction_id");
		int ts_then = GetLocalInt(e.runnable, "last_activity_at");
		int ts_now = GetUnixTimestamp();

		if (p && ts_then > 0 && (ts_now - ts_then) < PARTY_REJOIN_DIFF) {
			object other = GetPartyMemberByFactionId(p);

			if (is_valid(other)) {
				AddToParty(e.runnable, other);
				SendMessageToPC(e.runnable, "Du wurdest automagisch in deine alte Gruppe gebracht.");
			}
		}
	}

	if (e.ev == "player_hb" && !GetIsDM(e.runnable)) {
		int ts = GetUnixTimestamp();

		if (!GetIsDMPossessed(e.runnable)) {
			SetLocalInt(e.runnable, "faction_id", GetFactionId(e.runnable));
			SetLocalInt(e.runnable, "last_activity_at", ts);
		}
	}
}
